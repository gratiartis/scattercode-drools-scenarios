package uk.co.scattercode.drools.scenarios.ccygroups;

import java.util.ArrayList;

rule "Summarise currency group exposures"
	dialect "java"
when
	
	$gc: CurrencyGroupCurrency()

    // Ensure that we only generate a single group exposure for each group.
    not( CurrencyGroupExposure( group == $gc.group ) )
	
	$groupCurrencies : ArrayList() from accumulate (
			CurrencyGroupCurrency( group == $gc.group, $ccy: currency ),
			init( ArrayList list = new ArrayList(); ),
			action( list.add($ccy); ),
			reverse( list.remove($ccy); ),
			result(list)
	)
	
	accumulate( CurrencyExposure( currency memberOf $groupCurrencies, $exp : exposure );
			$sumExp : sum( $exp )
    )
    
then

	System.out.println("Group currencies: " + $groupCurrencies);
	System.out.println("Sum of exposures for group " + $gc.getGroup() + "=" + $sumExp);

	CurrencyGroupExposure gexp = new CurrencyGroupExposure($gc.getGroup(), $sumExp);
	
	insert(gexp);

end
